/*************************************************************************
  ko.cgs
  Combinatorial Game Suite Example Script
  
  Loads several example ko, including all the figures given in
  Berlekamp-Muller-Spight.
*************************************************************************/

simplekoL(m_,n_) := a:{m||a|n};
simplekoR(m_,n_) := a:{m|a||n};
iteratedkoB(mean_) := a:{{mean+2|a},simplekoR(mean+1,mean)|{a|mean-2},simplekoL(mean,mean-1)};
iteratedkoA(mean_) := LeftOptions(iteratedkoB(mean))[1];
iteratedkoC(mean_) := RightOptions(iteratedkoB(mean))[1];

/************************************
   Basic Examples
************************************/

nanoko := simplekoR(1,0);
nanoko2 := simplekoL(1,0);
thousandyear := a:{0,c:{23||c|-10}|0,b:{24|b||-9}};

/************************************
   Ko from Berlekamp-Muller-Spight
************************************/

// Simple Ko (39-41)

bms[39] := nanoko;
bms[40] := simplekoR(1,-2);
bms[41] := simplekoR(24,-9);

// Sente Ko (42-45)

bms[42] := a:{7|a,simplekoL(0,-1)||-2};
bms[43] := a:{7|{a|-2},simplekoL(0,-1)};
bms[44] := a:{3|a,simplekoL(0,-1)||-2};
bms[45] := a:{3|{a|-2},simplekoL(0,-1)};

// Throw-in Ko (46-47)

bms[46] := {1||iteratedkoB(0),{1|simplekoL(0,-1)}};
bms[47] := {3|{3||2|1},a:{{4|a},simplekoR(3,2)|}};

// 2-Iterated Ko (48-49)

bms[48] := iteratedkoC(1);
bms[49] := iteratedkoB(1);

// Corner-iterated Ko (50-52)

bms[50] := a:{ {2|a},simplekoR(1,0) || {a|simplekoR(0,-1)},simplekoL(0,-1) };
bms[51] := LeftOptions(bms[50])[1];
bms[52] := RightOptions(bms[50])[1];

// Real 2 (57-58 and 60)

bms[58] := a:{5|a,iteratedkoA(0)||{a|-3},simplekoL(-1,-2)};
bms[57] := RightOptions(bms[58])[1];
bms[60] := LeftOptions(bms[58])[1];

// Corridor Approaching Ko (59)

bms[59] := {4||||3|||2||1|-nanoko};

// Approach Ko (61-66)

bms65sub := a:{18|a,b:{-7|c:{b|-8}}||c};
bms[65] := a:{1|a,b:{1|c:{b|bms65sub}}||0,c};
bms[66] := RightOptions(bms[65])[2];

// Mannen Ko (67-69)

bms[67] := {0,a:{23||a|-9}|0,a:{23|a||-9}};
bms[68] := a:{0,simplekoL(17,-10)||a|simplekoR(19,-8)};
bms[69] := RightOptions(bms[68])[1];

bms[73] := {0 || a:{ simplekoL(7,-20) | {a|simplekoR(9,-18),-1} } };
bms[72] := {bms[73]|-20};
bms[75] := {0,a:{6|b:{a||b|-20}} | 0,c:{7|c||c|-19}};
bms[74] := {simplekoL(7,-20),bms[75]||simplekoL(8,-19)|-19};
bms[71] := {7|bms[73],bms[74]||-20};
bms[70] := {7|bms[71]};
bms[76] := a:{{3|0},simplekoL(3,-1)|{a|-1},simplekoL(0,-1)};
bms[79] := a:{1||a,simplekoR(1,0)|iteratedkoB(-1),{0|simplekoL(-1,-2)}};

bms[80] := {0,a:{3|b:{a|-1},simplekoL(0,-1)} || b|-2};
bms[81] := {a:{3|a,-nanoko||-1}|-2};
bms[80] := {0|bms[81]};
bms[82] := {{3|||0||-1|-3},{3|iteratedkoA(0)}||0,iteratedkoB(0)};
bms[83] := a:{{1|a},simplekoR(1,0)||{0|-8},simplekoR(1,-8)};
bms[84] := a:{4|a,simplekoL(1,0)||0};
bms[85] := a:{4|simplekoL(1,0),{a|0}};
bms[86] := a:{2|||a,{simplekoR(0,-1)|-10}||-10|-20};
bms[87] := RightOptions(bms[86])[1];
bms[89] := {-1,iteratedkoB(-1)||iteratedkoC(-1)|-4};
bms[88] := {2|bms[89]};
// NOTE: why does cgsuite simplify the {|0||} ?
bms[90] := {5||simplekoR(2,1)|0|||-1|-2};

bms[99] := a:{8| {simplekoR(7,6)|} ||{-2|-6},a|||-6};
bms[98] := {7|6||bms[99]};
bms[97] := {bms[98]|-6};

bms[103] := simplekoR(5,{3|-1});
bms[107] := {6|2|| {2|0} , a:{7|a,simplekoL(2,1)||simplekoR(2,1)} |||-2};

/******************************************************
   Assorted Superko.
   NOTE: These will not produce proper thermographs.
******************************************************/

simpledoubleko := a:{8|b:{a,{8|b}|21}};
cdkT := T:{4||T|3};
cdkU := U:{4|U||3};
cdkV := V:{3||V,a:{2|a||1}|-10};
cdkW := W:{ a:{2||a|1} || W , b:{2|b||1} | -10 };
cdkX := X:{2||X|1};
complexdoubleko := D1:{
      cdkU , DL : { cdkT | D1 , D2:{2,DL|cdkW,DR:{cdkX,D1,D2|-11}} }
    | cdkV , DR
    };
molassesko := a:{i:{60|a} || b:{61|b, d:{g:{60|g,i||-25}|d||-24} ||-24}};
reducedmko := a:{i:{60|a} || d:{i|d||-24}};
abstractgame1 := A1:{
      C1:{27||C1|-9}
    | B:{A1,A2:{C2:{9||C2|-3}|B}|-50}
    };
abstractgame2 := LeftOptions(RightOptions(abstractgame1)[1])[2];

/**********************
   User Instructions
**********************/

"The following ko have been loaded successfully:" >> out;
"" >> out;
t := CreateTable(2);
AddTableRow(t, "nanoko", "A standard nano-ko");
AddTableRow(t, "thousandyear", "The thousand-year ko");
AddTableRow(t, "bms[n]", "Figure n in Berlekamp-Muller-Spight");
t >> out;
"" >> out;
"For example, type Plot(Thermograph(bms[65] : Environment := \"RightKomaster\")) " +
"to see the thermograph of BMS Figure 44 with Right as Komaster." >> out;
"Or, type KoPlot(bms[65]) to see both Komaster thermographs on the same plot." >> out;
