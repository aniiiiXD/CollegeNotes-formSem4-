/*************************************************************************
  day3.cgs
  Combinatorial Game Suite Example Script
  by Dan Hoey
  
  Calculates the number of games born on day 3.
*************************************************************************/

iscomparabletoany := proc(g,L) local h;
  for h in L do
    if not g <> h then return true fi
  od;
  false
end;

indepsubs := proc(S) local PS, g, L, newsub;
  PS := [[]];
  for g in S do
    for L in PS do
      if not iscomparabletoany(g,L) then
        // Form a new list by adding g
        newsub := Clone(L);
        Add(newsub, g);
        Add(PS, newsub);
      fi
    od
  od;
  PS
end;

eqsets := proc(L1,L2) local g;
  if (Length(L1) != Length(L2)) then false
  else
    for g in L1 do
      if not Contains(L2,g) then return false fi
    od;
    true
  fi
end;

allgamesof := proc(S) local lo, ro, all, g;
  all := [];
  for lo in S do
    for ro in S do
      g := {lo|ro};
      if eqsets(LeftOptions(g),lo) and eqsets(RightOptions(g),ro) then
        // g is in canonical form
        Add(all, g)
      fi
    od
  od;
  all
end;

dayn(0) := [0];
for n from 1 to 3 do
  dayn(n) := allgamesof(indepsubs(dayn(n-1)))
od;
Length(dayn(3))
